Sub OlejTransformatorowy()
    ' Sprawdź, czy aktywny arkusz to "OlejTransformatorowy"
    If ActiveSheet.Name <> "OlejTransformatorowy" Then
        MsgBox "Ten kod działa tylko w arkuszu 'OlejTransformatorowy'.", vbExclamation
        Exit Sub
    End If
    
    Dim userInput As String
    Dim ws As Worksheet
    Dim indeks As Integer
    Dim odpowiedz As VbMsgBoxResult
    Dim AmountOfDigits As Integer
    Dim ScoursOfMeasurement() As Double
    Dim AverageOfVoltage As Double, sum As Double, varianceSum As Double, AverageStandardVariation As Double, RelativeStandardDeviation As Double
    Dim AmountOfMeasurement As Integer
    Dim i As Integer

    ' Czyszczenie arkusza przed przetwarzaniem danych
    Set ws = ThisWorkbook.ActiveSheet
    
    odpowiedz = MsgBox("Czy chcesz wyczyścić arkusz przed przetwarzaniem danych?", vbYesNo + vbQuestion, "Potwierdzenie")

    If odpowiedz = vbYes Then
        ws.Cells.Clear
        MsgBox "Arkusz został wyczyszczony.", vbInformation
    ElseIf odpowiedz = vbNo Then
        MsgBox "To są aktualne dane.", vbInformation
    End If

    If WorksheetFunction.CountA(ws.Cells) = 0 Then
        MsgBox "Arkusz jest pusty.", vbInformation
    Else
        ws.Activate
    End If

    ' Wczytywanie ilości miejsc po przecinku dla pomiarów
    Do
        userInput = InputBox("Wprowadź ilość miejsc po przecinku dla pomiarów:", "Ilość miejsc", "Podaj liczbę całkowitą dodatnią")
        If userInput = "" Then Exit Sub
        If IsNumeric(userInput) And Val(userInput) > 0 And Int(userInput) = userInput Then
            AmountOfDigits = CInt(userInput)
            Exit Do
        Else
            MsgBox "Podano niepoprawną wartość. Podaj liczbę całkowitą dodatnią."
        End If
    Loop

    ' Ustawienie nagłówków kolumn
    With ws
        .Cells(1, 1).Value = "L.p."
        .Cells(1, 2).Value = "Wartość napięcia przebicia [kV]"
        .Cells(1, 3).Value = "Wartość średnia napięcia przebicia [kV]"
        .Cells(1, 4).Value = "Średnie odchylenie standardowe [kV]"
        .Cells(1, 5).Value = "Względne odchylenie standardowe [%]"
        
        ' Ustawienie szerokości kolumn
        .Columns(1).AutoFit
        .Columns(2).AutoFit
        .Columns(3).AutoFit
        .Columns(4).AutoFit
        .Columns(5).AutoFit
        .Columns(3).NumberFormat = "0." & String(AmountOfDigits, "0")
        .Columns(4).NumberFormat = "0." & String(AmountOfDigits, "0")
        .Columns(5).NumberFormat = "0." & String(AmountOfDigits, "0")
    End With

    ' Wczytywanie ilości pomiarów
    Do
        userInput = InputBox("Wprowadź ilość pomiarów:", "Ilość pomiarów napięcia przebicia oleju transformatorowego", "Podaj liczbę całkowitą dodatnią")
        If userInput = "" Then Exit Sub
        If IsNumeric(userInput) And Val(userInput) > 0 And Int(userInput) = userInput Then
            AmountOfMeasurement = CInt(userInput)
            Exit Do
        Else
            MsgBox "Podano niepoprawną wartość. Podaj liczbę całkowitą dodatnią."
        End If
    Loop

    ReDim ScoursOfMeasurement(1 To AmountOfMeasurement)

    ' Wczytywanie pomiarów
    For i = 1 To AmountOfMeasurement
        userInput = InputBox("Podaj pomiar " & i & ":")
        If userInput = "" Then Exit Sub
        If IsNumeric(userInput) Then
            ScoursOfMeasurement(i) = CDbl(userInput)
        Else
            MsgBox "Podano niepoprawną wartość. Podaj liczbę."
            Exit Sub
        End If
    Next i

    ' Wstawianie pomiarów do arkusza
    For i = 1 To AmountOfMeasurement
        ws.Cells(i + 1, 1).Value = i
        ws.Cells(i + 1, 2).Value = ScoursOfMeasurement(i)
    Next i

    ' Obliczenia
    sum = WorksheetFunction.sum(ScoursOfMeasurement)
    AverageOfVoltage = sum / AmountOfMeasurement
    ws.Cells(2, 3).Value = AverageOfVoltage
    
    For i = 1 To AmountOfMeasurement
        varianceSum = varianceSum + (ScoursOfMeasurement(i) - AverageOfVoltage) ^ 2
    Next i
    
    AverageStandardVariation = Sqr(varianceSum / (AmountOfMeasurement - 1))
    ws.Cells(2, 4).Value = AverageStandardVariation
    
    RelativeStandardDeviation = (AverageStandardVariation / AverageOfVoltage) * 100
    ws.Cells(2, 5).Value = RelativeStandardDeviation

    ' Scalanie komórek
    Dim zakres As Range
    Set zakres = ws.Range("C2:C" & AmountOfMeasurement + 1)
    With zakres
        .MergeCells = True
        .VerticalAlignment = xlCenter
    End With
    Set zakres = ws.Range("D2:D" & AmountOfMeasurement + 1)
    With zakres
        .MergeCells = True
        .VerticalAlignment = xlCenter
    End With
    Set zakres = ws.Range("E2:E" & AmountOfMeasurement + 1)
    With zakres
        .MergeCells = True
        .VerticalAlignment = xlCenter
    End With
   ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
   
   
    Dim tabela As Range
    ' Ustaw arkusz, na którym pracujemy
    Set ws = ActiveSheet

    ' Znajdź obszar tabeli zawierający dane
    Set tabela = ws.Cells.CurrentRegion

    ' Dodaj obramowanie do granic tabeli
    With tabela.Borders
        .LineStyle = xlContinuous ' Styl linii
        .Color = vbBlack ' Kolor linii
        .Weight = xlThin ' Grubość linii
    End With
   ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
   
    Dim rng As Range
    Dim cell As Range
    ' Określ zakres na całym arkuszu
    Set rng = ws.UsedRange

    ' Iteruj przez każdą komórkę w zakresie
    For Each cell In rng
        ' Sprawdź, czy komórka jest wypełniona
        If Not IsEmpty(cell.Value) Then
            ' Wyśrodkuj zawartość wypełnionej komórki
            cell.HorizontalAlignment = xlCenter
        End If
    Next cell
   
End Sub


